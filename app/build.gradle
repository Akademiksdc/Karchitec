apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "com.paradigmadigital.karchitect"
        minSdkVersion 16
        targetSdkVersion 25

        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "4g"
    }
}

//kapt {
//    generateStubs = true
//}

tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

ext {
    support = '25.3.1'
    constraint = '1.0.2'
    arch = '1.0.0-alpha1'

    retrofit = '2.1.0'
    converter_gson = '2.0.0'
    okhttp = '3.6.0'
    butterknife = '8.5.1'
    dagger = '2.5'
    picasso = '2.5.2'
    timber = '4.5.1'
    android_chart = 'v3.0.1'

    junit = '4.11'
    robolectric = '3.3'
    assertj = '1.1.1'
    mockito = '2.7.14'
    mockito_kotlin = '1.3.0'
    espresso = '2.2.2'
    mockwebserver = '3.6.0'
    runner = '0.5'
    commons = '2.4'
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "com.android.support:design:$support"
    compile "com.android.support:support-annotations:$support"
    compile "com.android.support.constraint:constraint-layout:$constraint"

    compile "android.arch.persistence.room:runtime:$arch"
    compile "android.arch.lifecycle:runtime:$arch"
    compile "android.arch.lifecycle:extensions:$arch"
    kapt "android.arch.persistence.room:compiler:$arch"
    kapt "android.arch.lifecycle:compiler:$arch"

    kapt "com.jakewharton:butterknife-compiler:$butterknife"
    compile "com.jakewharton:butterknife:$butterknife"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    compile "com.google.dagger:dagger:$dagger"

    compile "com.squareup.okhttp3:okhttp:$okhttp"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp"
    compile "com.squareup.retrofit2:retrofit:$retrofit"
    compile "com.squareup.retrofit2:converter-gson:$converter_gson"
    compile "com.squareup.picasso:picasso:$picasso"
    compile "com.jakewharton.timber:timber:$timber"

    testCompile "junit:junit:$junit"
    testCompile "org.robolectric:robolectric:$robolectric"
    testCompile("com.squareup.assertj:assertj-android:$assertj") {
        exclude module: 'support-annotations'
    }
    testCompile "org.mockito:mockito-core:$mockito"
    testCompile "com.squareup.okhttp3:mockwebserver:$mockwebserver"
    testCompile "commons-io:commons-io:$commons"

    androidTestCompile("com.android.support.test:runner:$runner") {
        exclude module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso") {
        exclude module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-intents:$espresso") {
        exclude module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espresso") {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-annotations'
        exclude module: 'design'
    }
}